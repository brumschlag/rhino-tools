[DelimitedRecord("\t")]
class Customers:
	CustomerID as string
	CompanyName as string
	ContactName as string
	ContactTitle as string
	Address as string
	City as string
	Country as string
	
connection( 
	"Database",
	ConnectionType: SqlConnection,
	ConnectionString: "Data Source=localhost;Initial Catalog=ETL_Test; Integrated Security=SSPI;"
	)
	
	
source CustomersFile:
	execute:
		file = Read(typeof(Customers)).From("""Files\CustomersTab.txt""")
		for customer in file:
			SendRow( Row( 
				CustomerId: customer.CustomerID, 
				CompanyName: customer.CompanyName,
				ContactName: customer.ContactName
				)) 
		file.Dispose()

source OrdersFromDatabase, Connection="Database":
	Command: "SELECT * FROM Orders"
	
join CustomersAndOrders:
	if Left.CustomerID == Right.CustomerID:
		Row.OrderID = Right.OrderID
		Row.CompanyName = Left.CompanyName
		Row.ContactName = Left.ContactName
		
destination Final, Connection="Database":
	Command: "INSERT INTO OrdersWareHousing VALUES(@OrderID, @CompanyName,@ContactName)"
		
pipeline OrdersWareHouse:
	CustomersFile >> CustomersAndOrders.Left
	OrdersFromDatabase >> CustomersAndOrders.Right
	CustomersAndOrders >> Final
	
target default:
	Execute("OrdersWareHouse")